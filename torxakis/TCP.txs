-- Delays are in milliseconds, percentages are 0-100
TYPEDEF Tampering ::=
	Delay { delay :: Int }
	| JitteredDelay { jitterDelay :: Int ; jitter :: Int }
	| PacketDrops { loss :: Int }
	| LowThroughput { rate :: Int ; burst :: Int ; latency :: Int }
	| Reordering { reorder :: Int ; correlation :: Int }
	| Duplication { duplicate :: Int } 
	| Bitflips { corrupt :: Int }
ENDDEF

TYPEDEF TamperingPair ::=
	TamperingPair { first :: Tampering ; second :: Tampering }
ENDDEF

-- A packet is encoded as a string for now
TYPEDEF PacketList ::=
	Packet_nil
	| Packet_cons { head :: String ; tail :: PacketList }
ENDDEF

FUNCDEF lengthPacketList (pl :: PacketList) :: Int ::=
	IF isPacket_nil(pl)
	THEN 0
	ELSE lengthPacketList(tail(pl)) + 1
	FI
ENDDEF

FUNCDEF isValid_Tampering (t :: Tampering) :: Bool ::=
	IF isDelay(t)
	THEN (delay(t) > 0) /\ (delay(t) < 400)
	ELSE
		IF isJitteredDelay(t)
		THEN (jitterDelay(t) > 0) /\ (jitterDelay(t) < 400) /\ (jitter(t) > 0) /\ (jitter(t) < 320) 
		ELSE
			IF isPacketDrops(t)
			THEN (loss(t) > 0) /\ (loss(t) < 55)
			ELSE
				IF isLowThroughput(t)
				THEN (rate(t) > 0) /\ (rate(t) < 400) /\ (burst(t) > 0) /\ (burst(t) < 1028) /\ (latency(t) > 0) /\ (latency(t) < 500)
				ELSE
					IF isReordering(t)
					THEN (reorder(t) > 0) /\ (reorder(t) < 55) /\ (correlation(t) > 0) /\ (correlation(t) < 70)
					ELSE
						IF isDuplication(t)
						THEN (duplicate(t) > 0) /\ (duplicate(t) < 55)
						ELSE
							IF isBitflips(t)
							THEN (corrupt(t) > 0) /\ (corrupt(t) < 55)
							ELSE True
							FI
						FI
					FI
				FI
			FI
		FI
	FI
ENDDEF

FUNCDEF isValid_TamperingPair (tp :: TamperingPair) :: Bool ::=
	isValid_Tampering(first(tp)) /\ isValid_Tampering(second(tp))
		/\ not (isDelay(first(tp)) /\ isJitteredDelay(second(tp)))
		/\ not (isJitteredDelay(first(tp)) /\ isDelay(second(tp)))
	\/
	isValid_Tampering(first(tp)) /\ isValid_Tampering(second(tp))
		/\ not (isDelay(first(tp)) /\ isReordering(second(tp)))
		/\ not (isReordering(first(tp)) /\ isDelay(second(tp)))
ENDDEF

CHANDEF Channels ::=
	TamperingIn :: Tampering ;
	PacketsIn :: PacketList ;
	PacketsOut :: PacketList
ENDDEF

MODELDEF TCP ::=
	CHAN IN  TamperingIn, PacketsIn
	CHAN OUT PacketsOut
	
	BEHAVIOUR
		TamperingIn ? x >-> PacketsIn ? y >-> PacketsOut ! y
ENDDEF

CNECTDEF Sut ::=
	CLIENTSOCK
	
	CHAN OUT PacketsIn HOST "localhost" PORT 7890
	ENCODE PacketsIn ? x -> ! toString(x)
	
	CHAN OUT TamperingIn HOST "localhost" PORT 7891
	ENCODE TamperingIn ? x -> ! toString(x)
	
	CHAN IN PacketsOut HOST "localhost" PORT 7890
	DECODE PacketsOut ! fromString(x) <- ? x
ENDDEF
